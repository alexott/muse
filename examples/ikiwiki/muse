#!/usr/bin/perl
# Ikiwiki plugin for Emacs Muse.
# Author: Michael Olson
# License: GPLv2 or later
#
# In your ikiwiki.setup file, set the muse_init option to the location
# of the init file for Muse.  Some examples provided in this directory
# are muse-init-simple.el and muse-init-project.el.

package IkiWiki::Plugin::muse;

use warnings;
use strict;
use IkiWiki 2.00;
use Encode;
use File::Temp;

sub import {
    hook(type => "htmlize", id => "muse", call => \&htmlize);
}

sub htmlize (@) {
    my %params=@_;
    my $content = decode_utf8(encode_utf8($params{content}));
    my $qname = $params{page};
    $qname =~ s/"/\\"/g;

    my ($fh, $filename) = File::Temp::tempfile();
    print $fh $content;
    close $fh;
    my $qfile = $filename;
    $qfile =~ s/"/\\"/g;
    eval {
        system qw( emacs -q --no-site-file -batch -l ),
               $config{muse_init}, '--eval',
               qq{(muse-ikiwiki-publish-file "$qfile" "$qname")};
        {
            open my $ifh, '<', $filename;
            local $/;
            $content = <$ifh>;
            close $ifh;
        }
    };
    unlink $filename;
    return $content;
}

sub test {
    print htmlize(content => "<example>\nHello\n</example>\n\nParagraph.\n",
                  page => "some_page.muse");
}

1
